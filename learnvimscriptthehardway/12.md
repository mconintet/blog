##自动命令

现在我们将讨论一个和映射同样主要的主题：自动命令（autocommands）。

自动命令是一种能让 Vim 在特定的事件发生的时候去执行特定命令的功能。让我们看一个例子。

通过 `:edit foo` 去打开一个新的文件，然后直接通过 `:quit` 去关闭它。查看硬盘你会发现没有新建文件。这是因为 Vim 实际上没有创建文件，除非你首次的时候保存它。

让我们改变这个情况，使得 Vim 可以见你编辑它们（不存在的文件）时就会自动的创建它们。运行下面的命令：

```vim
:autocmd BufNewFile * :write
```

这里内容比较多，不过我们可以先试试它，看看它是如何工作的。再次运行 `:edit foo` ，通过 `:quit` 去关闭它，这是看看硬盘。这次将会新建（当然了，是一个空的文件）。

我们将通过关闭 Vim 去移除上面的自动命令。后面一节中我们将讨论如何避免这个情况。

##自动命令的结构

让我们仔细的看看我们刚才创建的自动命令：

```vim
:autocmd BufNewFile * :write
		 ^          ^  ^
		 |          |  |
		 |          |  需要运行的命令
		 |          |
		 |          一个过滤事件的模式
		 |
		 需要被监视的事件
```

命令的第一部分就是我们需要监视的事件。Vim 提供了很多可以让我监视的事件。它们的中的一些包括：
- 开始编辑一个不存在的文件
- 读取一个文件，不管它是否存在
- 切换缓冲区的 `filetype` 设置
- 一段时间内没有任何的键盘按键被按下
- 进入插入模式
- 离开插入模式

这些只是可用的事件的一小部分。还有很多事件，你可以用它们做很多有趣的事情。

命令的下一个部分是一个 “模式（pattern）”，它可以让你更加详细的指定命令何时被触发。开启一个新的 Vim 实例然后运行下面的代码：

```vim
:autocmd BufNewFile *.txt :write
```

这个命令几乎和上一个差不多，但是这次它将只作用于以 `.txt` 结尾的文件。

运行 `:edit bar` 然后 `:quit` 来试试它，然后 `:edit bar.txt` 和 `:quit`。你可以看到 Vim 自动的写入了 `bar.txt`，但是没有写入 `var`，因为它不匹配上面的模式。

命令的最后一部分是一个你想当事件发生时运行的命令。这是很容易理解的，但是有一个问题：你不能在命令中使用特殊字符，比如 `<cr>`。我们将在下一节中讨论关于这个限制，目前就由他去吧。

##另一个例子

让我定义另一个自动命令，这次使用一个不同的事件。运行下面的命令：

```vim
:autocmd BufWritePre *.html :normal gg=G
```

我们有点超前了，因为我们将在以后才讨论关于 `normal`，不过现在你得相信我，它在此时这是一个很好的例子。

创建一个叫做 `foo.html` 的文件。使用 Vim 编辑它并原样输入下面的文本：

```vim
<html>
<body>
 <p>Hello!</p>
				</body>
				 </html>
```

现在通过 `:w` 去保存它。发生了什么？Vim 似乎在保存它之前对其进行了重新缩进！

现在我希望你相信我，运行 `:normal gg=G` 将会告诉 Vim 你要重新缩进当前的文件。现在不必担心它是如何工作的。

我们需要关心这个自动命令的哪一部分？它的事件类型是 `BufWritePre`，这意味着事件检查将会发生在*所有*文件写入之前。

我们使用了模式 `*.html` 去确保这个命令只有在文件以 `.html` 结尾时才会被触发。这可以让我们将自动命令指定到特定的文件，这是一个非常有用的功能，我们稍后将会解释它。

##多个事件

你可以创建一个绑定到多个事件的自动命令，通过使用逗号去分隔事件。运行下面的代码：

```vim
:autocmd BufWritePre,BufRead *.html :normal gg=G
```

这和我们上一个命令差不多，除了它在我们读取一个 HTML 文件的时候也会重新缩进它。这是有用的，当你有一个合作伙伴没有很好的缩进时。

一个 Vim 脚本中约定俗成的配对就是 `BufRead` 和 `BufNewFile`，每当你打开一种特定类型的文件，不论它是否存在。运行下面的代码：

```vim
:autocmd BufNewFile,BufRead *.html setlocal nowrap
```

这会在你工作于 HTML 文件时关闭自动换行。

##文件类型事件

其中一个最长用的事件是 `FileType`。这是事件发生在 Vim 设置缓冲区的 `filetype` 时。

让我们为不同的文件类型设置一些有用的映射。

```vim
:autocmd FileType javascript nnoremap <buffer> <localleader>c I//<esc>
:autocmd FileType python     nnoremap <buffer> <localleader>c I#<esc>
```

打开一个 Javascript 文件（以 `.js` 结尾的文件），找一行然后输入 `<localleader>c`。这将会注释这一行。

现在打开一个 Python 文件（以 `.py` 结尾的文件），找一行然后输入 `<localleader>c`。这将注释掉这一行，不过是使用的 Python 的注释字符。

使用自动命令配合上一节中的局部缓冲区映射，可以创建适用于正在编辑的文件的映射。

这样可以在编码时减轻我们大脑的负荷。可以通过 “注释这一行” 去简单的注释一行，而不是需要考虑移动到行首然后输入注释字符。

##练习

使用 `:help autocmd-events` 去查看所有可以绑定自动命令的事件。你现在不必去逐一记住它们。只要简单的看看你可以通过它们做什么。

创建一些 `FileType` 自动命令并使用 `setlocal` 去按你的方式去为你喜欢的文件类型设置选项。一些你可能需要为不同类型文件而设置的选项 `wrap`，`list`，`spell`，和 `number`。

为你经常工作的文件类型创建一些 “注释这一行” 的自动命令。

将所有这些自动命令添加到你的 `~/.vimrc` 文件中。当然，需要使用快捷键去编辑和应用它！
