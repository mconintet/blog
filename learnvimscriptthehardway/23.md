##函数

和大多数的编程语言一样，Vimscript 也是有函数的。让我们看看如何创建它们，以及关于它们的怪癖。

运行下面的命令：

```vim
:function meow()
```

你可能认为这是在定义一个名叫 `meow` 的函数。不幸的是你猜错了，我们已经进去了 Vimscript 的一个怪癖。

**Vimscript 的函数，如果是未限定作用域的， *必须* 以大写字母开头！**

甚至是你在函数上面附加了其作用域（我们之后将会讨论），我可能最好还是将函数名称的首字母大写。大部分的 Vimscript 代码编写者们都是这么做的，所以别打破这个约定。

好了，让我们实实在在的定义一个函数。运行下面的命令：

```vim
:function Meow()
:  echom "Meow!"
:endfunction
```

这次 Vim 很开心的定义了一个函数。让我们试着运行它：

```vim
:call Meow()
```

Vim 将会显示 `Meow!`，这个预期一样。

让我们试着返回一个值。运行下面的命令：

```vim
:function GetMeow()
:  return "Meow String!"
:endfunction
```

现在通过下面的命令去试试：

```vim
:echom GetMeow()
```

Vim 将会调用上面的函数并将其结果交给 `echom`，这将会显示 `Meow String!`

##调用函数

你可以看到在 Vimscript 中两用不同的调用函数的方式了。

当你希望直接调用一个函数的时候使用 `call` 方法。运行下面的命令：

```vim
:call Meow()
:call GetMeow()
```

第一个将会显示 `Meow!`，但是第二个将什么都不显示。当使用 `call` 的时候返回值将会被丢弃，所以它只有在函数是单方面作用的情况下才是有用的。

第二种调用函数的方式是使用表达式方式。这次你不需要使用 `call` ，你只需要使用函数名。运行下面的命令：

```vim
:echom GetMeow()
```

和我们之前看到的一样，这会调用到 `GetMeow` 并将其返回值交给 `echom`。

##隐式返回值

运行下面的命令：

```vim
:echom Meow()
```

这将会显示两行：`Meow!` 和 `0`。第一行很显然来自 `Meow` 中的 `echom`。第二行告诉我们，如果 Vimscript 的函数没有显式的返回值，它将隐式的返回 `0`。让我们使用下这个特性。运行下面的命令：

```vim
:function TextwidthIsTooWide()
:  if &l:textwidth ># 80
:    return 1
:  endif
:endfunction
```

这个函数使用了许多我们已经看过的 Vimscript 中重要点：
- `if` 语句
- 把选项当成变量
- 局部设置选项
- 大小写敏感的比较

如果你对这些还很陌生，你应该回头再看看它们。

现在我们已经定义了一个函数，它可以告诉我们在当前的缓冲区设置中，`textwidth` 是不是 “太宽了”（80 个字符对于除了 HTML 来说，都是正确的宽度）。

然我们试着使用它。运行下面的命令：

```vim
:set textwidth=80
:if TextwidthIsTooWide()
:  echom "WARNING: Wide text!"
:endif
```

我们这里做了什么？
- 首先我们将全局的 `textwidth` 设置成了 `80`。
- 我们运行了一个 if 语句，看看 `TextwidthIsTooWide()` 是否为真
- 结果为假，所以 if 语句体没有被执行到

因为我们没有显式的返回一个值，Vim 返回了 `0`，这是一个假值。让我们试着改变它。运行下面的命令：

```vim
:setlocal textwidth=100
:if TextwidthIsTooWide()
:  echom "WARNING: Wide text!"
:endif
```

这次函数中的 if 语句执行了它的语句体，返回了 `1`，所以我们手动输入的 if 执行了它的语句体。

##练习

阅读 `:help :call`。现在忽略任何关于 `ranges` 的东西。你可以往函数中提交多少参数？这让你感到惊讶吗？

阅读 `:help E124` 的第一段，并找出哪些字符是可以组成合法的函数名，下划线是可以的吗？破折号呢？重音字符呢？Unicode 字符呢？如果文档说的不清楚，手动的试一试。

阅读 `:help return`。这个命令的简短形式是什么（记得我让你不要使用命令的缩写形式吗）？这是你期望的吗？如果不是，为什么？
