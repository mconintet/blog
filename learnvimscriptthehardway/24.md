##函数参数

Vimscript 当然可以接收参数。运行下面的命令：

```vim
:function DisplayName(name)
:  echom "Hello!  My name is:"
:  echom a:name
:endfunction
```

运行下面的函数：

```vim
:call DisplayName("Your Name")
```

Vim 将会显示两行：`Hello! My name is:` 和 `Your Name`。

注意我们传递给 `echom` 的参数名前面有一个 `a:` 。这表示了变量的作用域，我们在之前的章节中讨论过。

让我们移除这个作用域前缀，看看 Vim 将会作何表现。运行下面的命令：

```vim
:function UnscopedDisplayName(name)
:  echom "Hello!  My name is:"
:  echom name
:endfunction
:call UnscopedDisplayName("Your Name")
```

这次 Vim 抱怨说它无法找到那个变量 `name`。

当你写一个 Vimscript 函数需要接收参数的时候，你 *总是* 需要在参数前面加上前缀 `a:`，以此去告知 Vim 它们的作用域是在参数域中。

##多个参数

Vimscript 可以接受变长参数列表，和 Javascript，Python 一样。运行下面的命令：

```vim
:function Varg(...)
:  echom a:0
:  echom a:1
:  echo a:000
:endfunction

:call Varg("a", "b")
```

这个函数告诉了我们很多信息，所以让我们一个一个看。

函数定义中的 `...` 告诉 Vim 那个函数可以接收任意数量的参数。这就好像 Python 函数中的 `*args`。

函数的第一行输出了 `a:0` 的消息，显示了 `2`。当你在 Vim 中定义了一个接收可变参数的函数时，`a:0` 显示的是所有接收到的参数个数。这这个例子中我们传递了两个参数到 `Varg` 所以 Vim 显示了 `2`。

第二行显示了 `a:1` 值是 `a`。你可以使用 `a:1`，`a:2` 一次类推的去引用到其他的接收参数。如果在上面中使用了 `a:2` 那么 Vim 将会显示 `b`。

第三行就有点棘手。当函数有可变参数的时候 `a:000` 将会是一个包含所有传递参数的一个列表。我们还没有接触过列表。对于列表不能使用 `echom`，这就是为什么我们使用了 `echo`。

你可以将边长参数和一般参数搭配起来使用。运行下面的命令：

```vim
:function Varg2(foo, ...)
:  echom a:foo
:  echom a:0
:  echom a:1
:  echo a:000
:endfunction

:call Varg2("a", "b", "c")
```

你可以看到 Vim 将 `"a"` 放到了命名参数 `a:foo` 中，然后其他的参数被放置到了可变参数列表中。

##赋值

试着运行下面的命令：

```vim
:function Assign(foo)
:  let a:foo = "Nope"
:  echom a:foo
:endfunction

:call Assign("test")
```

Vim 将会抛出一个错误，因为你不能对变量重新赋值。现在运行下面的命令：

```vim
:function AssignGood(foo)
:  let foo_tmp = a:foo
:  let foo_tmp = "Yep"
:  echom foo_tmp
:endfunction

:call AssignGood("test")
```

这次函数可以工作了，Vim 显示了 `Yep`。

##练习

阅读 `:help function-argument` 的前两段。

阅读 `:help local-variables`。
