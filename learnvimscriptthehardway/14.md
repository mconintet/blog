##自动命令组

之前的执行章节中我们学习了自动命令。运行下面的代码：

```vim
:autocmd BufWrite * :echom "Writing buffer!"
```

现在通过 `:write` 去写入当前缓冲区，并运行 `:messages` 去看消息日志。你可以在消息列表中看到 `Writing buffer!`。

现在再次写入当前缓冲区后运行 `:messages` 去查看消息日志。你可以看到 `Writing buffer!` 在消息列表中出现了两次。

现在再次运行这个相同的命令：

```vim
:autocmd BufWrite * :echom "Writing buffer!"
```

写入当前文件两次并运行 `:messages`。你将看到 `Writing buffer!` 出现了*四*次。发生了了？

看起来 Vim 无法知道当你在创建一个自动命令的时候是否需要替换现有的。在我们的情况中，Vim 创建了两个做了同一件事情的*独立*的自动命令。

##问题

现在我们知道了很可能会创建重复的命令，你可能会想：“怎样？只要别那么做就行！”。

问题就是当你应用你的 `~/.vimrc` 文件会重新阅读整个这个文件，包括任何一个你已经定义过的自动命令！这就意味着每当你应用你的 `~/.vimrc` 文件时，你将重复定义自动命令，这将使得 Vim 的运行变得缓慢，因为它不止一次的运行同一个命令。

为了模拟这个情况，试着运行下面的命令：

```vim
:autocmd BufWrite * :sleep 200m
```

现在写入当前文件。你已经注意或没注意到这轻微的延迟。现在再运行这个命令三次：

```vim
:autocmd BufWrite * :sleep 200m
:autocmd BufWrite * :sleep 200m
:autocmd BufWrite * :sleep 200m
```

再次写入。这次延迟就会变得更加的明显了。

显然的，你不会只用自动命令去做 sleep，但是对于一个 Vim 常用者来说，`~/.vimrc` 文件很容易的就会超过 1，000 行，它们中的大部分都会是自动命令。加上已安装插件中的自定义命令，这肯定会影响性能的。

##自动命令分组

对于上面的问题 Vim 提供了一个解决方案。第一步就是我们需要把相关操作的自动命令放到一个命名的分组中。

新开一个 Vim 实例去清除之前定义的自动命令，然后运行下面的代码：

```vim
:augroup testgroup
:    autocmd BufWrite * :echom "Foo"
:    autocmd BufWrite * :echom "Bar"
:augroup END
```

中间两行的缩进是可有可无的，如果你不愿意的话可以不用输入。

现在写入缓冲区并检查 `:messages`。你可以看到 `Foo` 和 `Bar`。现在运行下面的代码：

```vim
:augroup testgroup
:    autocmd BufWrite * :echom "Baz"
:augroup END
```

试着猜猜当你再次写入缓冲区的时候会将发生什么。一旦你脑中有了一个答案，你就可以写入缓冲区并检查 `:messages` 去看看你的答案是否正确。

##清空分组

当你写入文件的时候发生什么？和你期望的一样吗？

如果你认为 Vim 将会替换分组的话，你可以看到你的猜想错了。别担心，大部分人都会这么认为（因为我当初也是）。

如果你多次使用 `augroup`，Vim 每次都会合并分组。

如果你希望*清空（clear）*一个分组的话，你可以在分组的内部使用 `autocmd!`。运行下面的代码：

```vim
:augroup testgroup
:    autocmd!
:    autocmd BufWrite * :echom "Cats"
:augroup END
```

现在试着写入文件并检查 `:messages`。这次当你写入文件的时候， Vim 只输出了 `Cats`。

##在你的 Vimrc 中使用自动命令

现在我们知道如何将自动命令分组并清空分组，我们可以使用这个方式去往 `~/.vimrc` 中添加自动命令，这样就不会在每次应用 `~/.vimrc`  的时候都添加一个重复定义的。

将下面的添加到你的 `~/.vimrc` 文件中：

```vim
augroup filetype_html
    autocmd!
	autocmd FileType html nnoremap <buffer> <localleader>f Vatzf
augroup END
```

我们进入了一个 `filetype_html` 的分组，立刻清空了它，定义了一个自动命令，然后离开。如果我们再次应用 `~/.vimrc` 文件，清空操作将会防止 Vim 去添加一个重复的自动命令。

##练习

将你的 `~/.vimrc` 中的自动命令都像上面那样使用分组包裹起来。你可以按需要将多个自动命令放到同一个分组中。

试着弄明白上面最后一个例子中的映射是什么意思。

阅读 `:help autocmd-groups`。
